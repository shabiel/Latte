KBANLUT1 ; VEN/SMH - Unit tests for Latte ;2017-07-04  12:11 PM
 ;;3.0;KBAN LATTE;;;Build 12
 ;
 ; (c) Sam Habiel 2013
 ;
 ; Usage is granted to the user under accompanying license.
 ; If you can't find the license, you are not allowed to use the software.
 ;
TEST ; M-Unit Entry point for Unit Testing the MOCHA Interface
 W $C(9),$$RED,"Latte checks inside of VISTA",!!,$$RESET
 ; SAM!: THIS IS TEMPORARY. PUT THAT BACK!
 ;N KBANFORCEINT S KBANFORCEINT=1 ; Force checked to be inside of VISTA.
 S IO=$PRINCIPAL
 N DIQUIET S DIQUIET=1
 D DT^DICRW
 D EN^%ut($T(+0),3)
 ;
 ;W !!
 ;W $C(9),$$RED,"Latte checks against DIT",!!,$$RESET
 ;K KBANFORCEINT
 ;S IO=$PRINCIPAL
 ;N DIQUIET S DIQUIET=1
 ;D DT^DICRW
 ;D EN^%ut($T(+0),1)
 QUIT
 ;
CONCHK ; @TEST - Connection Check
 N STATUS S STATUS=$$PINGTST^PSSHRIT()
 D CHKEQ^%ut(STATUS,0,"Connection check failed.")
 QUIT
 ;
INTERTST ; @TEST - Drug-Drug Interaction Check
 ; I 'KBANFORCEINT,'$$FIND1^DIC(18.12,,"XQ","DIT DI SERVICE","B") QUIT
 N PSORDER,PSDRUG1,PSDRUG2,BASE,INFO,PSSPEC
 S BASE=$T(+0)
 K ^TMP($J,BASE)
 S ^TMP($J,BASE,"IN","DRUGDRUG")=""
 S PSORDER="I;1464P;PROSPECTIVE;2",PSDRUG1="WARFARIN 10MG TAB",PSDRUG2="ASPIRIN 325MG TAB"
 SET ^TMP($JOB,BASE,"IN","PROSPECTIVE","I;1464P;PROSPECTIVE;2")="6559^4005201^^WARFARIN 10MG TAB"
 SET ^TMP($JOB,BASE,"IN","PROSPECTIVE","I;91464P;PROSPECTIVE;2")="4376^4005735^^ASPIRIN 325MG TAB"
 D IN^PSSHRQ2(BASE)
 ;
 S INFO=$G(^TMP($J,BASE,"OUT","DRUGDRUG","S",PSDRUG1,PSORDER,1,"PMON",9,0)) ;
 I INFO="" S INFO=$G(^TMP($J,BASE,"OUT","DRUGDRUG","C",PSDRUG1,PSORDER,1,"PMON",9,0)) ; ven/smh for DIT -- different severity.
 D CHKTF^%ut($L(INFO),"Drug-drug interaction check failed")
 QUIT
 ;
DUPTHRPY ; @TEST - Duplicate therapy check
 N PSORDER,PSDRUG1,PSDRUG2,BASE,CLAS1,CLAS2,LINE1,LINE2
 N PSSLEFT S PSSLEFT=4     ; left margin for results
 S BASE=$T(+0)
 K ^TMP($J,BASE)
 S ^TMP($J,BASE,"IN","THERAPY")=""
 S PSORDER="O;403931;PROFILE;3"
 S PSDRUG1="CIMETIDINE 300MG TAB"
 S PSDRUG2="RANITIDINE 150MG TAB"
 S ^TMP($J,BASE,"IN","PROFILE","O;403931;PROFILE;3")="11666^4006826^^CIMETIDINE 300MG TAB^O"
 S ^TMP($J,BASE,"IN","PROSPECTIVE","Z;1;PROSPECTIVE;1")="11673^4007038^^RANITIDINE 150MG TAB"
 D IN^PSSHRQ2(BASE)
 ; 
 S CLAS1=$G(^TMP($J,BASE,"OUT","THERAPY",1,1,"CLASS"))
 S CLAS2=$G(^TMP($J,BASE,"OUT","THERAPY",1,2,"CLASS"))
 D CHKTF^%ut($L(CLAS1)!$L(CLAS2),"Duplicate therapy check failed")
 QUIT
 ;
CUSTOM ; @TEST - Custom drug-drug interaction check
 N INFO,INTRO,ORDER,DRUG1,DRUG2,BASE,STATUS,PSSPEC
 S INFO=""
 S BASE=$T(+0)
 S ORDER="Z;1;PROSPECTIVE;1"
 S DRUG1="CLARITHROMYCIN 250MG TAB",DRUG2="DIAZEPAM 5MG TAB"
 K ^TMP($J,BASE)
 S ^TMP($J,BASE,"IN","DRUGDRUG")=""
 S ^TMP($J,BASE,"IN","PROSPECTIVE","Z;1;PROSPECTIVE;1")="16373^4010075F^^CLARITHROMYCIN 250MG TAB"
 S ^TMP($J,BASE,"IN","PROFILE","I;10U;PROFILE;10")="3768^4002216^^DIAZEPAM 5MG TAB" ; VEN/SMH - Changed b/c VUID wrong in FOIA
 D IN^PSSHRQ2(BASE)
 I $D(^TMP($J,BASE,"OUT","DRUGDRUG","S",DRUG1,ORDER,1)) S INFO=$G(^TMP($J,BASE,"OUT","DRUGDRUG","S",DRUG1,ORDER,1,"PMON",9,0))
 E  I $D(^TMP($J,BASE,"OUT","DRUGDRUG","S",DRUG2,"I;10U;PROFILE;10",1)) S INFO=$G(^TMP($J,BASE,"OUT","DRUGDRUG","S",DRUG2,"I;10U;PROFILE;10",1,"PMON",9,0))
 D CHKTF^%ut($L(INFO),"Custom drug-drug interaction check")
 QUIT
 ;
MASSTEST ; @TEST - Test interactions of 30 drugs together
 N START S START=$P($H,",",2)
 N PSDRUG
 N BASE S BASE=$T(+0)
 K ^TMP($J,BASE)
 S ^TMP($J,BASE,"IN","THERAPY")=""
 S ^TMP($J,BASE,"IN","DRUGDRUG")=""
 S PSDRUG(1)="CIMETIDINE 300MG TAB"
 S PSDRUG(2)="SIMVASTATIN 20MG TAB"
 S PSDRUG(3)="RANITIDINE HCL 150MG TAB"
 S PSDRUG(4)="WARFARIN NA 5MG TAB"
 S PSDRUG(5)="AMLODIPINE BESYLATE 10MG/ATORVASTATIN CA 40MG TAB"
 S PSDRUG(6)="KETOCONAZOLE 2% CREAM,TOP"
 S PSDRUG(7)="TRIAMCINOLONE ACETONIDE 0.1% CREAM,TOP"
 S PSDRUG(8)="DIGOXIN 0.125MG TAB"
 S PSDRUG(9)="ALBUTEROL SO4 0.083% INHL,3ML"
 S PSDRUG(10)="TIOTROPIUM 18MCG CAP,INHL,30"
 S PSDRUG(11)="AMIODARONE HCL 200MG TAB"
 S PSDRUG(12)="CARVEDILOL 12.5MG TAB"
 S PSDRUG(13)="METOPROLOL SUCCINATE 100MG TAB,SA"
 S PSDRUG(14)="SELENIUM SULFIDE 1% SHAMPOO"
 S PSDRUG(15)="RAMIPRIL 5MG CAP"
 S PSDRUG(16)="CIPROFLOXACIN 500MG TAB,SA"
 S PSDRUG(17)="METHADONE HCL 10MG TAB,UD"
 S PSDRUG(18)="ALPRAZOLAM 0.5MG TAB"
 S PSDRUG(19)="FLUOXETINE HCL 20MG CAP"
 S PSDRUG(20)="OLANZAPINE 2.5MG TAB"
 S PSDRUG(21)="CALCIUM 600MG (AS CARBONATE)/VITAMIN D 400UNIT TAB"
 S PSDRUG(22)="METHOCARBAMOL 750MG TAB"
 S PSDRUG(23)="SIMETHICONE 125MG TAB,CHEW"
 S PSDRUG(24)="ACETAMINOPHEN 500MG TAB"
 S PSDRUG(25)="DOXYLAMINE SUCCINATE 25MG TAB"
 S PSDRUG(26)="SIMETHICONE 125MG TAB,CHEW"
 S PSDRUG(27)="ENTACAPONE 200MG TAB"
 S PSDRUG(28)="BENZTROPINE MESYLATE 1MG/ML INJ"
 S PSDRUG(29)="ROPINIROLE HCL 1MG TAB"
 S PSDRUG(30)="CARBIDOPA 25MG/LEVODOPA 100MG TAB,SA"
 S ^TMP($J,BASE,"IN","PROFILE",$$SUBBLD("I",,1))=$$NODEBLD(PSDRUG(1))
 N I F I=1:0 S I=$O(PSDRUG(I)) Q:'I  D
 . S ^TMP($J,BASE,"IN","PROSPECTIVE",$$SUBBLD("Z",1,I))=$$NODEBLD(PSDRUG(I))
 D IN^PSSHRQ2(BASE)
 N END S END=$P($H,",",2)
 W "Time elapsed: "_(END-START),!
 ;
 ; DEBUG
 ; ZWRITE ^TMP($J,BASE,*)
 ; Just assert that we didn't crash.
 D CHKTF^%ut(+^TMP($J,BASE,"OUT",0)'=-1,"Check succeeded")
 ; DEBUG
 ;
 QUIT
 ;
PROFILET ; @TEST - Test that profile drug interactions show up in non-prospective only checks.
 N START S START=$P($H,",",2)
 N PSDRUG
 N BASE S BASE=$T(+0)
 K ^TMP($J,BASE)
 S ^TMP($J,BASE,"IN","THERAPY")=""
 S ^TMP($J,BASE,"IN","DRUGDRUG")=""
 S ^TMP($J,BASE,"IN","PROFILEVPROFILE")="" ; Prospective only = false
 S PSDRUG(1)="CIMETIDINE 300MG TAB"
 S PSDRUG(2)="SIMVASTATIN 20MG TAB"
 S PSDRUG(3)="RANITIDINE HCL 150MG TAB"
 S PSDRUG(4)="WARFARIN NA 5MG TAB"
 S PSDRUG(5)="AMLODIPINE BESYLATE 10MG/ATORVASTATIN CA 40MG TAB"
 S ^TMP($J,BASE,"IN","PROFILE",$$SUBBLD("R",,1))=$$NODEBLD(PSDRUG(1))
 S ^TMP($J,BASE,"IN","PROFILE",$$SUBBLD("R",,2))=$$NODEBLD(PSDRUG(2))
 S ^TMP($J,BASE,"IN","PROFILE",$$SUBBLD("R",,3))=$$NODEBLD(PSDRUG(4))
 S ^TMP($J,BASE,"IN","PROFILE",$$SUBBLD("O",,4))=$$NODEBLD(PSDRUG(5))
 S ^TMP($J,BASE,"IN","PROSPECTIVE",$$SUBBLD("Z",1,1))=$$NODEBLD(PSDRUG(3))
 D IN^PSSHRQ2(BASE)
 N END S END=$P($H,",",2)
 ; W !,"Time elapsed: "_(END-START),!
 ;
 ; DEBUG
 ; ZWRITE ^TMP($J,BASE,*)
 ; DEBUG
 ;
 N IXNTRUE S IXNTRUE=0
 I $D(^TMP($J,BASE,"OUT","DRUGDRUG","C",PSDRUG(1)))!($D(^TMP($J,BASE,"OUT","DRUGDRUG","C",PSDRUG(4)))) S IXNTRUE=1
 D CHKTF^%ut(IXNTRUE,"Cimetidine is supposed to show up in message")
 ;
 QUIT
 ;
PROSTEST ; @TEST - Test that profile drug interactions etc don't show up in Prospective only checks.
 N START S START=$P($H,",",2)
 N PSDRUG
 N BASE S BASE=$T(+0)
 K ^TMP($J,BASE)
 S ^TMP($J,BASE,"IN","THERAPY")=""
 S ^TMP($J,BASE,"IN","DRUGDRUG")=""
 ; S ^TMP($J,BASE,"IN","PROFILEVPROFILE")="" ; Prospective only = true now
 S PSDRUG(1)="CIMETIDINE 300MG TAB"
 S PSDRUG(2)="SIMVASTATIN 20MG TAB"
 S PSDRUG(3)="RANITIDINE HCL 150MG TAB"
 S PSDRUG(4)="WARFARIN NA 5MG TAB"
 S PSDRUG(5)="AMLODIPINE BESYLATE 10MG/ATORVASTATIN CA 40MG TAB"
 S ^TMP($J,BASE,"IN","PROFILE",1)=$$NODEBLD(PSDRUG(1))
 S ^TMP($J,BASE,"IN","PROFILE",2)=$$NODEBLD(PSDRUG(2))
 S ^TMP($J,BASE,"IN","PROFILE",3)=$$NODEBLD(PSDRUG(4))
 S ^TMP($J,BASE,"IN","PROFILE",4)=$$NODEBLD(PSDRUG(5))
 S ^TMP($J,BASE,"IN","PROSPECTIVE",1)=$$NODEBLD(PSDRUG(3))
 D IN^PSSHRQ2(BASE)
 N END S END=$P($H,",",2)
 ; W !,"Time elapsed: "_(END-START),!
 ;
 D CHKTF^%ut('$D(^TMP($J,BASE,"OUT","DRUGDRUG","C",PSDRUG(1))),"Cimetidine is not supposed to show up in message")
 ;
 QUIT
 ;
SINGLET ; @TEST - Make sure that a single drug does not trip off an error
 N PSDRUG
 N BASE S BASE=$T(+0)
 K ^TMP($J,BASE)
 S ^TMP($J,BASE,"IN","THERAPY")=""
 S ^TMP($J,BASE,"IN","DRUGDRUG")=""
 S PSDRUG(1)="CIMETIDINE 300MG TAB"
 S ^TMP($J,BASE,"IN","PROSPECTIVE",1)=$$NODEBLD(PSDRUG(1))
 D IN^PSSHRQ2(BASE)
 ;
 D CHKEQ^%ut(^TMP($J,BASE,"OUT",0),0,"No output is expected")
 ;
 QUIT
 ;
DNOTCHK1 ; @TEST - Test Drugs not checked 1: Profile drug missing VUID
 N START S START=$P($H,",",2)
 N PSDRUG
 N BASE S BASE=$T(+0)
 K ^TMP($J,BASE)
 S ^TMP($J,BASE,"IN","THERAPY")=""
 S ^TMP($J,BASE,"IN","DRUGDRUG")=""
 S PSDRUG(1)="CIMETIDINE 300MG TAB"
 S PSDRUG(2)="SIMVASTATIN 20MG TAB"
 S PSDRUG(3)="RANITIDINE HCL 150MG TAB"
 S PSDRUG(4)="WARFARIN NA 5MG TAB"
 S PSDRUG(5)="AMLODIPINE BESYLATE 10MG/ATORVASTATIN CA 40MG TAB"
 S ^TMP($J,BASE,"IN","PROFILE",$$SUBBLD("O",,1))=$$NODEBLD(PSDRUG(1))
 S ^TMP($J,BASE,"IN","PROFILE",$$SUBBLD("O",,2))=$$NODEBLD(PSDRUG(2))
 S ^TMP($J,BASE,"IN","PROFILE",$$SUBBLD("O",,3))=$$NODEBLD(PSDRUG(4))
 S ^TMP($J,BASE,"IN","PROFILE",$$SUBBLD("O",,4))=$$NODEBLD(PSDRUG(5))
 S ^TMP($J,BASE,"IN","PROSPECTIVE",$$SUBBLD("O",1,1))=$$NODEBLD(PSDRUG(3))
 ;
 S $P(^TMP($J,BASE,"IN","PROFILE",$$SUBBLD("O",,2)),U,2)="" ; Remove VUID from Simvastatin
 ;
 D IN^PSSHRQ2(BASE)
 N END S END=$P($H,",",2)
 ; W $C(9)_$$GREEN()_"Time elapsed: "_(END-START)_$$RESET()
 ;
 ; Descend two down and grab the node... should contain SIMVASTATIN.
 N KBANTRUE S KBANTRUE=0
 D
 . I '$D(^TMP($J,BASE,"OUT","EXCEPTIONS")) QUIT
 . N S1,S2 S S1=$O(^("EXCEPTIONS","")),S2=$O(^(S1,""))
 . N N S N=^(S2) I N["SIMVASTATIN" S KBANTRUE=1
 ;
 D CHKTF^%ut(KBANTRUE,"Simvastain should be found in the exception node")
 ;
 QUIT
 ;
DNOTCHK2 ; @TEST - Test Drugs not checked 2: Prospective drug missing VUID
 N START S START=$P($H,",",2)
 N PSDRUG
 N BASE S BASE=$T(+0)
 K ^TMP($J,BASE)
 S ^TMP($J,BASE,"IN","THERAPY")=""
 S ^TMP($J,BASE,"IN","DRUGDRUG")=""
 S PSDRUG(1)="CIMETIDINE 300MG TAB"
 S PSDRUG(2)="SIMVASTATIN 20MG TAB"
 S PSDRUG(3)="RANITIDINE HCL 150MG TAB"
 S PSDRUG(4)="WARFARIN NA 5MG TAB"
 S PSDRUG(5)="AMLODIPINE BESYLATE 10MG/ATORVASTATIN CA 40MG TAB"
 S ^TMP($J,BASE,"IN","PROFILE",$$SUBBLD("O",,1))=$$NODEBLD(PSDRUG(1))
 S ^TMP($J,BASE,"IN","PROFILE",$$SUBBLD("O",,2))=$$NODEBLD(PSDRUG(2))
 S ^TMP($J,BASE,"IN","PROFILE",$$SUBBLD("O",,3))=$$NODEBLD(PSDRUG(4))
 S ^TMP($J,BASE,"IN","PROFILE",$$SUBBLD("O",,4))=$$NODEBLD(PSDRUG(5))
 S ^TMP($J,BASE,"IN","PROSPECTIVE",$$SUBBLD("O",1,1))=$$NODEBLD(PSDRUG(3))
 ;
 S $P(^TMP($J,BASE,"IN","PROSPECTIVE",$$SUBBLD("O",1,1)),U,2)="" ; Remove VUID from Prospective drug
 ;
 D IN^PSSHRQ2(BASE)
 N END S END=$P($H,",",2)
 ; W !,"Time elapsed: "_(END-START),!
 ;
 ; Descend two down and grab the node... should contain Ranitidine.
 N KBANTRUE S KBANTRUE=0
 D
 . I '$D(^TMP($J,BASE,"OUT","EXCEPTIONS")) QUIT
 . N S1,S2 S S1=$O(^("EXCEPTIONS","")),S2=$O(^(S1,""))
 . N N S N=^(S2) I N["RANITIDINE" S KBANTRUE=1
 ;
 D CHKTF^%ut(KBANTRUE,"Ranitidine should be found in the exception node")
 ;
 QUIT
 ;
DXIXT ; @TEST - Test drugs that are excluded from interacting
 N BASE S BASE=$T(+0)
 K ^TMP($J,BASE)
 S ^TMP($J,BASE,"IN","THERAPY")=""
 S ^TMP($J,BASE,"IN","DRUGDRUG")=""
 N PSDRUG
 S PSDRUG(1)="HYDROCORTISONE ACETATE 1% OINT,TOP"
 S PSDRUG(2)="TRIAMCINOLONE ACETONIDE 0.1% PASTE,DENTAL"
 S PSDRUG(3)="KETOCONAZOLE 2% SHAMPOO"
 S PSDRUG(4)="ATORVASTATIN CA 80MG TAB"
 S ^TMP($J,BASE,"IN","PROSPECTIVE",$$SUBBLD("O",1,1))=$$NODEBLD(PSDRUG(1))
 S ^TMP($J,BASE,"IN","PROSPECTIVE",$$SUBBLD("O",1,2))=$$NODEBLD(PSDRUG(2))
 S ^TMP($J,BASE,"IN","PROSPECTIVE",$$SUBBLD("O",1,3))=$$NODEBLD(PSDRUG(3))
 S ^TMP($J,BASE,"IN","PROSPECTIVE",$$SUBBLD("O",1,4))=$$NODEBLD(PSDRUG(4))
 D IN^PSSHRQ2(BASE)
 D CHKTF^%ut('$D(^TMP($J,BASE,"OUT","DRUGDRUG","S","TRIAMCINOLONE ACETONIDE 0.1% PASTE,DENTAL")),"TAC dental shouldn't be in the drug interaction section")
 QUIT
 ;
SUPPLYT ; @TEST - Test drugs that can't be found in RxNorm.
 ; Quit if we are just using VISTA internally.
 Q:$D(KBANFORCEINT)
 N BASE S BASE=$T(+0)
 K ^TMP($J,BASE)
 S ^TMP($J,BASE,"IN","THERAPY")=""
 S ^TMP($J,BASE,"IN","DRUGDRUG")=""
 N PSDRUG
 S PSDRUG(1)="DRESSING,PRIMAPORE 11.75IN X 4IN"
 S PSDRUG(2)="WINGS CONTOURED INCONTINENT BRIEF LARGE"
 S ^TMP($J,BASE,"IN","PROSPECTIVE",$$SUBBLD("O",1,1))=$$NODEBLD(PSDRUG(1))
 S ^TMP($J,BASE,"IN","PROSPECTIVE",$$SUBBLD("O",1,2))=$$NODEBLD(PSDRUG(2))
 D IN^PSSHRQ2(BASE)
 D CHKTF^%ut($D(^TMP($J,BASE,"OUT","EXCEPTIONS")),"Exceptions expected because drugs move to not checked list.")
 QUIT
 ;
SIMERRT ; @TEST - Simulate an error and see that we degrade gracefully
 N BASE S BASE=$T(+0)
 K ^TMP($J,BASE)
 S ^TMP($J,BASE,"IN","THERAPY")=""
 S ^TMP($J,BASE,"IN","DRUGDRUG")=""
 N PSDRUG
 S PSDRUG(1)="TRIAMCINOLONE ACETONIDE 0.05% OINT,TOP"
 S PSDRUG(2)="ATORVASTATIN CA 80MG TAB"
 S ^TMP($J,BASE,"IN","PROSPECTIVE",$$SUBBLD("O",1,1))=$$NODEBLD(PSDRUG(1))
 S ^TMP($J,BASE,"IN","PROSPECTIVE",$$SUBBLD("O",1,2))=$$NODEBLD(PSDRUG(2))
 N KBANSIMERR S KBANSIMERR=1 ; Force an error to occur in KBANLDIT...
 D IN^PSSHRQ2(BASE)
 D CHKEQ^%ut(+^TMP($J,BASE,"OUT",0),-1,"Error condition should be reported")
 QUIT
 ;
DITERRT ; @TEST - Make sure DIT doesn't fail on drugs with different routes...
 ; Quit if we are just using VISTA internally.
 Q:$D(KBANFORCEINT)
 N BASE S BASE=$T(+0)
 K ^TMP($J,BASE)
 S ^TMP($J,BASE,"IN","THERAPY")=""
 S ^TMP($J,BASE,"IN","DRUGDRUG")=""
 N PSDRUG
 S PSDRUG(1)="TRIAMCINOLONE ACETONIDE 0.05% OINT,TOP"
 S PSDRUG(2)="SIMVASTATIN 20MG TAB"
 S ^TMP($J,BASE,"IN","PROSPECTIVE",$$SUBBLD("O",1,1))=$$NODEBLD(PSDRUG(1))
 S ^TMP($J,BASE,"IN","PROSPECTIVE",$$SUBBLD("O",1,2))=$$NODEBLD(PSDRUG(2))
 D IN^PSSHRQ2(BASE)
 D CHKEQ^%ut(^TMP($J,BASE,"OUT",0),0,"No output expected.")
 QUIT
NODEBLD(DRUGNM) ; Build node for drug based on drug name.
 N VAP S VAP=$O(^PSNDF(50.68,"B",DRUGNM,""))
 I 'VAP S $EC=",U-NO-VAP-FOUND,"
 I $L($$GET1^DIQ(50.68,VAP,"INACTIVATION DATE")) S $EC=",U-VAP-INACTIVE,"
 I $$GET1^DIQ(50.68,VAP,"MASTER ENTRY FOR VUID")'="YES" S $EC=",U-VAP-NO-MASTER,"
 N GCN S GCN=$$GET1^DIQ(50.68,VAP,"GCNSEQNO")
 N VUID S VUID=$$GET1^DIQ(50.68,VAP,"VUID")
 Q GCN_U_VUID_U_VAP_U_DRUGNM_U_$R(2233)_U_"O"
 ;
SUBBLD(TYPE,PROS,NO) ; Build subscript node for testing
 Q TYPE_";"_5555_";"_$S($G(PROS):"PROSPECTIVE",1:"PROFILE")_";"_NO
 ;
GREEN() Q $C(27)_"[1;32m" ; VT100 text green bold
RESET() Q $C(27)_"[0m" ; VT100 reset
RED() Q $C(27)_"[1;31m" ; VT100 red bold
